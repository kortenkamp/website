
<!DOCTYPE html>
<html lang="en">
	<head><link rel="stylesheet" href="../style.css">
		<meta charset="utf-8">
    <title>Cindy JS</title>
		<script type="text/javascript" src="../../build/js/Cindy.js"></script>
    <script type="text/javascript" src="../../build/js/CindyGL.js"></script>
  </head>
    
	<body style="margin:0; background:#000000;">
    <script id="csinit" type="text/x-cindyscript">
      use("CindyGL");
      
			width = 960;
			height = 540;
			
      L = [0, 0]; //bottom left corner
      R = [width, 0]; //bottom right corner
  
			
			acharge = .7;
			bcharge = -.7;
			
			scharge = 0;
			
			forall(["Cfract","N","Nb","LIC"],
				createimage(#, width, height);
			);
      
      colorplot(L, R, "Cfract", (random(), random(), 0)); //the fractional parts of the coordinates (Cx, Cy) (see paper)
      colorplot(L, R, "N", round(random()));


      pixel2coord(x) := x/width*4+(-2,-1.125);
      field(x) := (
				regional(f1,f2,a,b,z,E);
        f1=(x-A)/(|(x-A)|^3); //3dimensional point charge
        f2=(x-B)/(|(x-B)|^3);
				
				//segment
				//http://physicstasks.eu/659/charged-line-segment
				relpos = mat*x-offset;
				b = |P0-P1|-relpos.x;
				a = -relpos.x;
				z = relpos.y;
				E = (1/|(b,z)|-1/|(a,z)|, 1/z*(b/|(b,z)|-a/|(a,z)|));
				
				r=acharge*f1+bcharge*f2 + scharge*imat*E/|P0-P1|;
				if(|r|>4, r=4*r/|r|);
				r
        //s=abs(f);
      ); //vector field
			
			potential(x) := (
				max(-1,min(1, 
					
					
					//http://hyperphysics.phy-astr.gsu.edu/hbase/electric/potlin.html#c1
					relpos = mat*x-offset;
					b = |P0-P1|-relpos.x;
					a = -relpos.x;
					z = relpos.y;
					pot = re(log( (b+|(b,z)|)/(a+|(a,z)|) ));
					
					0.4*(acharge/|x-A|+bcharge/|x-B|+scharge*pot/|P0-P1|)
			));
			);

      h = width/600;
      getNewC(pc) := (
        col = imagergb(L, R, "Cfract", pc, interpolate->false);
        curC = (floor(pc.x)+col.x, floor(pc.y)+col.y);
        curC - h*field(pixel2coord(curC));  //euler step in the past
      );
			
		tpos = [1.4, 1.6, 1.8];//target pos
		pos = [1.6, 1.8, 1.4];//starting pos
		resetclock();
    </script>

    <script id="csdraw" type="text/x-cindyscript">
			expi(t):=0.3*(cos(t),sin(t));
			
		
			col(chargeorpot):= (
				//lambda = (charge+1)/2;
				//lambda*[1,0,0]+(1-lambda)*[0,1,0]
				if(chargeorpot>0,
					chargeorpot*[1,0,0],
					-chargeorpot*[0,1,0]
				)
			);
			
			//the 3 sliders at the left.
	
	/* //sort the sliders    
			slist = apply(sort([[A.x,1], [B.x,2], [S.x,3]], #_1),#_2);
			errc(slist);
			slistinv = [0,0,0];
			forall(1..3,j,
				forall(1..3,k, if(slist_k==j, slistinv_j=k));
			);
			pos = .6*pos+.4*apply(slistinv, i, tpos_i);
	*/		
	    PA.x = pos_1;
	    if (PA.y > .8, PA.y = .8);
	    if (PA.y < -.8, PA.y = -.8);
	    acharge = PA.y/.8;
	 

	    PB.x = pos_2;
	    if (PB.y > .8, PB.y = .8);
	    if (PB.y < -.8, PB.y = -.8);
	    bcharge = PB.y/.8;

	    PS.x = pos_3;
	    if (PS.y > .8, PS.y = .8);
	    if (PS.y < -.8, PS.y = -.8);
	    scharge = PS.y/.8;
	    
			
			//make it possible to "turn off" charges
			if(|acharge|<.05,acharge=0);
			if(|bcharge|<.05,bcharge=0);
			if(|scharge|<.05,scharge=0);
			
			acolor = col(acharge)+[1,1,1]/2;
			bcolor = col(bcharge)+[1,1,1]/2;
			scolor = col(scharge)+[1,1,1]/2;
			PA.color = acolor;
			PB.color = bcolor;
			PS.color = scolor;
			
			aalpha = max(0,1-2*|mouse()-A|);
			balpha = max(0,1-2*|mouse()-B|);
			salpha = max(0,1-2*|mouse()-S|);
			
			
			data = [acharge, bcharge, scharge, A, B, P0,P1];
			change = false;
			if(data!=odata,
				change = true;
			);
			odata = data;
			
			sv = P1.xy-P0.xy;
			sv = sv/|sv|;
			sn = (sv.y, -sv.x);
			mat = transpose([sv, sn]);
			imat = inverse(transpose([sv, sn]));
			offset = imat*(P0.xy);
		
			colorplot(L, R, "N",
				newC = getNewC(#);
				//if(|newC-#|>4, newC= #+4*(newC-#)/|newC-#|);
				if(random()<|newC-#|/20, newC = #+100*[sin(seconds()), cos(seconds())]); //sample from "random" position on screen
				newval =	if((newC.x > 0 & newC.y > 0 & newC.x<width & newC.y < height),
					imagergb(L, R, "N", newC, interpolate->false).r, //NEAREST interpolation.
					round(random())
				);
				//newval = if(abs(mouse().xy - pixel2coord(#)) < 0.1, 0, newval);
				//% |potential(pixel2coord(#))|>1
				if(random()<.001, random(), newval) //sometimes flip (noise injection to "counteract duplication effect")
			);


      alpha = 0.02;
      colorplot(L, R, "Nb", //exponentially blended images of linearly interpolated noise images
      	/*newC = getNewC(#);
      	Na =	if(newC.x > 0 & newC.y > 0 & newC.x<width & newC.y < height,
      			imagergb(L, R, "N", (newC.x, newC.y)).r, //linear interpolation
      			random()
      	);*/
        Na = imagergb(L, R, "N", #).r;
        alpha*Na + (1-alpha)*imagergb(L, R, "Nb", #).r
      );

      colorplot(L, R, "Cfract",
        newC = getNewC(#);
				//if(#.x>500,
				//(newC.x+.5-floor(newC.x+.5), newC.y+.5-floor(newC.y+.5), 0),
				(newC.x-floor(newC.x), newC.y-floor(newC.y), 0)
				//)
        
      );
      //drawimage([-2, -2], [2, -2], "Nb");
      st = 4/width;
			
			expand(v) := ( //make contrasts more intensive
				//.5+2*(v-.5)
				1/2-cos(v*pi)/2
			);
			
      colorplot([-2, -1.125], [2, -1.125], "LIC", //simple and slow Array LIC
         sum = (2*imagergb([-2, -1.125], [2, -1.125], "Nb", #).r);
				 xb = #; //base
				 sgn = 1;
         repeat(2,
					 xf = xb;
					 repeat(5, 
					 ff = field(xf);
					 if(|ff|>0,ff=ff/|ff|;);
            xf = xf + sgn*st*ff;//use normalized vectorfield

						sum = sum + if(false, round(random()), (imagergb([-2, -1.125], [2, -1.125], "Nb", xf, interpolate->false).r));
					);
					sgn = -1;
         );
				 pt = potential(#);
         gray(expand(sum/12))+col(pt)*.25;//-gray((10*pt);-floor(10*pt))/20
      );
			
			
      drawimage([-2, -1.125], [2, -1.125], "LIC");
		
			draw(A, color->acolor, alpha->.8, size->10);
			draw(B, color->bcolor, alpha->.8, size->10);
			drawtext(A+(0,-.045), if(acharge>0,"+","-"), alpha->|2*acharge|,color->[1,1,1],size->40,align->"mid");
			drawtext(B+(0,-.045), if(bcharge>0,"+","-"), alpha->|2*bcharge|,color->[1,1,1],size->40,align->"mid");
			
			
				fillpoly([[1.2,-4],[4,-4],[4,4],[1.2,4]], color->[0,0,0], alpha->.6);
			//sliders
			draw((pos_1, .8), (pos_1, -.8), color -> (1,1,1),size->2);
			draw((pos_2, .8), (pos_2, -.8), color -> (1,1,1),size->2);
			draw((pos_3, .8), (pos_3, -.8), color -> (1,1,1),size->2); 
			
			draw(PA, size->10, color->[1,1,1]);
			draw(PB, size->10, color->[1,1,1]);
			draw(PS, size->10, color->[1,1,1]);
			
			lunit = .07*(P0-P1)/|P0-P1|;
			lbase = (pos_3,.9);
			draw(lbase-lunit, lbase+lunit, color->[1,1,1], size->4, color->scolor);
			drawtext(lbase+(0,-.045), if(scharge>0,"+","-"), alpha->|2*scharge|,color->[1,1,1],size->40,align->"mid");
			
			abase = (pos_1, .9);
			bbase = (pos_2, .9);
			draw(abase, color->acolor, alpha->.8, size->10);
			draw(bbase, color->bcolor, alpha->.8, size->10);
			drawtext(abase+(0,-.045), if(acharge>0,"+","-"), alpha->|2*acharge|,color->[1,1,1],size->40,align->"mid");
			drawtext(bbase+(0,-.045), if(bcharge>0,"+","-"), alpha->|2*bcharge|,color->[1,1,1],size->40,align->"mid");
			
			
			forall(1..5,t,
				lambda = t/6;
				P = lambda*P0+(1-lambda)*P1;
				drawtext(P, if(scharge>0,"+","-"), alpha->|2*scharge|,color->[1,1,1],size->40,align->"mid");
			);
			l.color = scolor;	
    </script>

    <div id="CSCanvas" style="width: 960px; height:540px"></div>
    <script type="text/javascript">
        cdy = CindyJS({canvasname:"CSCanvas",
                    scripts: "cs*",
                    geometry:[
                      {name:"A", kind:"P", type:"Free", pos:[-.8  ,-.1],size:10, color:[1,1,1], alpha:.0},
											//{ name: "Acirc", type: "CircleByRadius", radius: 0.3, args: [ "A" ], color:[0,0,0], size: 0, pinned: true}, 											
											//{ name: "Aval", printname: "charge", type: "PointOnCircle", pos: [ .8,-.3 ], color: [ 1.0, 1.0, 1.0 ], narrow: true, color: [1,1,1], size:8, args:[ "Acirc" ], labeled: false }, 
									
                      {name:"B", kind:"P", type:"Free", pos:[.2 ,.2],size:10, color:[1,1,1], alpha:.0},
											//{ name: "Bcirc", type: "CircleByRadius", radius: 0.3, args: [ "B" ], color:[0,0,0], size: 0, pinned: true}, 											
											//{ name: "Bval", printname: "charge", type: "PointOnCircle", pos: [ -.4,.3 ], color: [ 1.0, 1.0, 1.0 ], narrow: true, color: [1,1,1], size:8, args:[ "Bcirc" ], labeled: false },
											
											//segment
											{name:"P0", kind:"P", type:"Free", pos:[-0.3  ,-.8],size:5, color:[1,1,1]},
											{name:"P1", kind:"P", type:"Free", pos:[.5  ,-.7],size:5, color:[1,1,1]},
											{name:"l", type:"Segment", args:["P0","P1"], color:[1,1,1], size:4, alpha:.7},
											
											{name:"S", type:"Mid", args:["P0","P1"], pos:[0 ,0],size:10, color:[1,1,1], alpha:.5, visible:false},
											//{ name: "Scirc", type: "CircleByRadius", radius: 0.3, args: [ "S" ], color:[0,0,0], size: 0, pinned: true}, 											
											//{ name: "Sval", printname: "charge", type: "PointOnCircle", pos: [ -.4,.3 ], color: [ 1.0, 1.0, 1.0 ], narrow: true, color: [1,1,1], size:8, args:[ "Scirc" ], labeled: false },
											
											
											{ name:"PA", kind:"P", type:"Free", pos: [.5,.5,1], narrow: 80, color: [1,1,1], size:8, alpha:0 },
                      { name:"PB", kind:"P", type:"Free", pos: [.5,-.5,1], narrow: 80, color: [1,1,1], size:8, alpha:0 },
                      { name:"PS", kind:"P", type:"Free", pos: [.5,0,1], narrow: 80, color: [1,1,1], size:8, alpha:0 }
                    ],
                    animation: {autoplay: true},
                    ports: [{
                      id: "CSCanvas",
											
									    
									    transform: [{visibleRect: [-2, -1.125, 2, 1.125]}],
                    }]
                  });
    </script>              
	</body>
</html>
