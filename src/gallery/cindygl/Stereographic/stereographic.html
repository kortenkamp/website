
<!DOCTYPE html>
<html>

<head>
<title>stereographic projection</title>
<meta charset="UTF-8">
<script type="text/javascript" src="https://cindyjs.org/dist/snapshot/Cindy.js"></script>
<script type="text/javascript" src="https://cindyjs.org/dist/snapshot/CindyGL.js"></script>
<style>
#CSCanvas {
    position: absolute;
    border: 0px;
    top: 0px;
    left: 0px;
    margin: 0px;
    width: 100vw;
    height: 100vh;
    background-color: rgb(0,0,0);
    /*width: 500px;
    height: 500px;*/
}
</style>
<script id="csinit" type="text/x-cindyscript">

 readcamera(latlon) := (
   imagergb([-pi,-pi/2], [pi,-pi/2], "earth", (-latlon_2, latlon_1), repeat->true);
 );

 readspec(latlon) := (
   imagergb([-pi,-pi/2], [pi,-pi/2], "earthspec", (-latlon_2, latlon_1), repeat->true)_1;
 );

rot(a, b) := (
  //https://math.stackexchange.com/questions/180418/calculate-rotation-matrix-to-align-vector-a-to-vector-b-in-3d/897677#897677
  v = cross(a,b);
  s = |v|;
  c = a*b;
  K = [[0, v_3, -v_2], [-v_3,0,v_1], [v_2, -v_1, 0]];
  I + K + K*K*(1-c)/(s^2)
);


flat2sphere(z) := (
  l2 = z*z;
  (2*z_1/(1+l2), 2*z_2/(1+l2), (-1+l2)/(1+l2))
);

sphere2flat(s) := (
  s = s/|s|;
  (s_1,s_2)/(1-s_3)
);

flat2latlon(z) := (
  //lambda = arctan2(re(z), im(z));
  //alpha = arctan(|z|);
  //2*alpha + pi/2-phi = pi
  //phi = 2*alpha - pi/2;
  (2*arctan(|z|)-pi/2, arctan2(z_2, z_1))
);

sphere2latlon(s) := (
  (re(arcsin(s_3)), arctan2(s_2, s_1))
);

floor2sphere(z) := (
  z = z/(1-floorh);
  l2 = |z|*|z|;
  (2*z_1/(1+l2), 2*z_2/(1+l2), (-1+l2)/(1+l2))
);


onScroll(delta) := (
  //errc(delta);
  //azoom = azoom-delta/1000
  p = p*exp(-delta/1000);
);

smoothstep(x) := (
  if(x<0, 0, if(x>1, 1, 3*x^2-2*x^3));
);

dragging = false;
I = [[1,0,0],[0,1,0],[0,0,1]];
mat = I;

dx = 0;
dy = 0;
sx = 0;
sy = 0;
p = (-3, 4, 4)/2;
floorh = -1.3;
showray = true;

endangle = 0;
endtime = seconds();
timeforpi = 3; //it takes so many seconds to rotate the sphere
mode = 0;
</script>


<script id="cskeydown" type="text/x-cindyscript">
errc(keycode());
if(keycode()==65,//a
  floorh = floorh + .1;
);
if(keycode()==66,//b
  floorh = floorh - .1;
);

if(keycode()==67,//c
  mode = 1-mode;
  if(mode==1,
    camera = cameravideo();
    readcamera(latlon) := (
      imagergb([-pi,-pi/2], [pi,-pi/2], camera, (-latlon_2, latlon_1), repeat->true);
    );
    readspec(latlon) := 0;
    ,
    
    readcamera(latlon) := (
      imagergb([-pi,-pi/2], [pi,-pi/2], "earth", (-latlon_2, latlon_1), repeat->true);
    );
    readspec(latlon) := (
      imagergb([-pi,-pi/2], [pi,-pi/2], "earthspec", (-latlon_2, latlon_1), repeat->true)_1;
    );
    
  );
);

if(keycode()==82,//r
  showray = !showray;
);

if(keycode()==73,//i -> invert
  if(endtime<seconds(), //only if we are almost finished with roation
    endangle = endangle + pi;
    endtime = max(endtime,seconds())+timeforpi;
  )
);
</script>

<script id="csdraw" type="text/x-cindyscript">
curangle = if(seconds()>=endtime, endangle, endangle-pi*smoothstep((endtime-seconds())/timeforpi));
rotmat = ((1, 0, 0),
    (0, cos(curangle), -sin(curangle)),
    (0, sin(curangle), cos(curangle)));

if (dragging,
    dx = 3 * (sx - mouse().x); dy = 3 * (sy - mouse().y);,
    //dx = .005 * cos(seconds() * .1); dy = .003 * sin(seconds() * .1);
    dx = dx*.9; dy=dy*.9;
);

sx = mouse().x;
sy = mouse().y;


p = 
/*(
    (1, 0, 0),
    (0, cos(dy), sin(dy)),
    (0, -sin(dy), cos(dy))
)**/
((cos(dx), -sin(dx), 0), (sin(dx), cos(dx), 0), (0,0,1))*p;
p_3 = p_3+5*dy;

if(p_3 < floorh+.1, p_3 = floorh+.1;);

v = cross((0,0,1), p);
w = cross(p, v);
v = v/|v|;
w = w/|w|;

if((mode == 0 & image ready("earth")) % (mode == 1 & image ready(camera)),
  colorplot(
    dir = (-p/|p|)+#.x*v+#.y*w;
    //ray(t) = p + t*dir
    
    b = 2*(p*dir);
    a = (dir*dir);
    c = (p*p)-1;
    
    d = b*b - 4*a*c;
    floort = (floorh - p_3)/dir_3;
    
    if(d>0 % floort>0,
      spheret = (-b - re(sqrt(d)))/(2*a);
      if(d>0 & (spheret<floort % floort<=0),
        s = p + spheret*dir;
        s = s/|s|;
        latlon = sphere2latlon(rotmat*s);
        readcamera(latlon)*1.2*(1+s_3/2)*(max(0,s*p/|p|)+1*readspec(latlon)*max(0,s*p/|p|)^12);

        ,
        
        floorxy = (p_1 + floort*dir_1, p_2 + floort*dir_2);
        latlon = sphere2latlon(rotmat*floor2sphere(floorxy));
        k = p - (floorxy_1, floorxy_2, floorh);
        k = k/|k|;
        readcamera(latlon)*(1/(1+.1*|floorxy|) + readspec(latlon)*max(0,k_3)^12)
      ),
      [0,0,0]
    )
  );
);


if(showray,
  draw(mouse());
  ndir = (0,0,1)-p;
  palpha = ndir*(-p/|p|);
  ndir = ndir/palpha;
  snorth = (ndir*v, ndir*w);

  dir = (-p/|p|)+mouse().x*v+mouse().y*w;
  floort = (floorh - p_3)/dir_3;
  floorxy = (p_1 + floort*dir_1, p_2 + floort*dir_2);
  
  intersect = floor2sphere(floorxy);
  idir = intersect-p;
  
  ialpha = idir*(-p/|p|);
  idir = idir/ialpha;
  sintersect = (idir*v, idir*w);

  if(intersect*p>0,
    draw(sintersect);
  );

  if((0,0,1)*p>0,
    draw(snorth);
  );
  
  draw(snorth, mouse());
  
draw(mouse());
//ray(t) = p + t*dir
)
</script>

<script id="csmousedown" type="text/x-cindyscript">
sx = mouse().x;
sy = mouse().y;

dragging = true;
</script>

<script id="csmouseup" type="text/x-cindyscript">
dragging = false;
</script>


<script type="text/javascript">
  var cdy = CindyJS({
    ports: [{id: "CSCanvas", transform:[{visibleRect:[-1,-1,1,1]}]}],
    scripts: "cs*",
    language: "en",
    images: {earth: "earth.jpg", earthspec: "earthspec.jpg"},
    autoplay: true,
    geometry: [],
    use: ["CindyGL"],
    oninit: function(){
      cdy.canvas.addEventListener("wheel", function() {
        cdy.evokeCS('onScroll(' + event.wheelDelta + ')');
      });
    }
  });
</script>
</head>

<body>
  <div id="CSCanvas" style="width:650px; height:650px; border:2px solid black"></div>
</body>
</html>
